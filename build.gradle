apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

version = '1.0'
mainClassName = 'co.fastestpath.api.FastestPathApplication'

repositories {
    mavenCentral()
}

dependencies {
    def dropwizardVersion = '1.0.0'

    compile 'io.dropwizard:dropwizard-core:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-forms:' + dropwizardVersion
    compile 'com.hubspot.dropwizard:dropwizard-guice:1.0.0.2'

    compile 'commons-io:commons-io:2.5'

    compile 'org.quartz-scheduler:quartz:2.1.0'

    compile 'org.jsoup:jsoup:1.9.1'

    compile 'net.lingala.zip4j:zip4j:1.2.9'

    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.8.1'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.0'

    compile 'org.springframework.data:spring-data-mongodb:1.9.6.RELEASE'

    testCompile 'io.dropwizard:dropwizard-testing:' + dropwizardVersion
    testCompile 'org.testng:testng:6.10'
}

test {
    useTestNG()
}

run {
    args = ['server', 'development.yml']
}

// intellij configuration
idea {
    module {
        name = 'fastestpath'

        jdkName = '1.8'

        sourceDirs += file('src/main/java')

        testSourceDirs += file('src/test/java')

        excludeDirs += file('.idea')
        excludeDirs += file('gradle')
    }
}

// creates bundled jar
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

// override jar task with shadowJar
tasks.jar.enabled = false
assemble.dependsOn(shadowJar)
