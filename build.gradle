apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = 'co.fastestpath.api.FastestPathApplication'

ext.dropwizardVersion = '1.0.0'

task wrapper(type: Wrapper) {
  gradleVersion = '4.3.1'
}

dependencies {
  compile project(':utils')
  compile project(':gtfs')
  compile project(':core')
}

buildscript {
  ext.kotlinVersion = '1.2.10'

  repositories {
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:' + kotlinVersion
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
  }
}

allprojects {
  apply plugin: 'idea'
  apply plugin: 'java'
  apply plugin: 'kotlin'

  version = '1.0'

  sourceCompatibility = 1.9
  targetCompatibility = 1.9

  repositories {
    jcenter()
    mavenCentral()
  }

  dependencies {
    compile 'commons-io:commons-io:2.5'

    compile 'org.jetbrains.kotlin:kotlin-stdlib'
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jre8'

    compile 'io.dropwizard:dropwizard-core:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-forms:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-logging:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-jackson:' + dropwizardVersion

    compile 'com.hubspot.dropwizard:dropwizard-guice:' + dropwizardVersion

    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:' + kotlinVersion

    compile 'javax.xml.bind:jaxb-api:2.3.0'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.8.1'

    testCompile 'org.testng:testng:6.10'
    testCompile 'org.mockito:mockito-core:2.8.47'
    testCompile 'io.dropwizard:dropwizard-testing:' + dropwizardVersion
  }

  test {
    useTestNG()
  }
}

run {
  args = ['server', 'development.yml']
}

shadowJar {
  mergeServiceFiles()
  exclude 'META-INF/*.SF'
  exclude 'META-INF/*.DSA'
  exclude 'META-INF/*.RSA'
}

jar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

// override jar task with shadowJar
tasks.jar.enabled = false
assemble.dependsOn(shadowJar)

idea {
  module {
    name = 'fastestpath'

    jdkName = '1.9'

    excludeDirs += file('.idea')
    excludeDirs += file('.gradle')
    excludeDirs += file('gradle')
    excludeDirs += file('out')
  }
}
